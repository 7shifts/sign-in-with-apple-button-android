apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'

def gitBranch() {
    def branch = ""
    def proc = "git rev-parse --abbrev-ref HEAD".execute()
    proc.in.eachLine { line -> branch = line }
    proc.err.eachLine { line -> println line }
    proc.waitFor()
    branch
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'com.7shifts.android'
            artifactId 'api'
            version gitBranch()

            artifact("$buildDir/outputs/aar/${artifactId}.aar")
        }
    }
}

artifactory {
    contextUrl = 'http://192.168.10.69:8081/artifactory'

    def artifactory_user = System.env.ARTIFACTORY_USER
    def artifactory_password = System.env.ARTIFACTORY_PASSWORD

    publish {
        repository {
            repoKey = 'gradle-dev-local'
            username = artifactory_user
            password = artifactory_password
        }
        defaults {
            publications('maven')
            publishArtifacts = true
            publishPom = true
            publishBuildInfo = true
        }
    }
    resolve {
        repository {
            repoKey = 'gradle-dev'
            username = artifactory_user
            password = artifactory_password
        }
    }
}

artifactoryPublish.dependsOn assemble
